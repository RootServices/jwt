// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'
apply plugin: 'signing'
apply plugin: 'maven'

group = "org.rootservices"
version = "1.3.1-SNAPSHOT"
archivesBaseName="jwt"

description = """JSON Web Tokens"""

sourceCompatibility = 1.9
targetCompatibility = 1.9

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    deployerJars
}

ext {
    jacksonVersion = '2.8.8'
    log4jVersion = '2.11.0'
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"

    testImplementation group: 'junit', name: 'junit', version:'4.12'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version:'1.3'
    testImplementation group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            if(!project.ext.properties.containsKey("ossrhUser")) {
                project.ext.ossrhUser = 'wrongUser'
                project.ext.ossrhPassword = 'wrongPassword'
            }

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
            pom.project {
                groupId 'org.rootservices'
                artifactId project.name
                version project.version
                name project.name
                description project.description
                packaging 'jar'
                url 'https://github.com/RootServices/jwt'

                scm {
                    connection 'git@github.com:RootServices/jwt.git'
                    developerConnection 'git@github.com:RootServices/jwt.git'
                    url 'https://github.com/RootServices/jwt'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://github.com/RootServices/jwt/blob/development/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'tmackenzie'
                        name 'Tom MacKenzie'
                        email 'tom@rootservices.org'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}
